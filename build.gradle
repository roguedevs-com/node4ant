apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6
group = 'com.roguedevs'
version = '0.1'
ext.packaging = 'jar'

repositories {
    mavenCentral()
}
dependencies {
    compile 'junit:junit:4.11'
    compile 'org.codehaus.groovy:groovy-all:2.0.5'
    compile 'org.apache.ant:ant:1.9.0'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

            //repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            repository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'node4ant'
                packaging 'jar'
                description 'Run node and related commands with ant'
                url 'https://github.com/roguedevs-com/node4ant'

                scm {
                    url 'scm:git@github.com:roguedevs-com/node4ant.git'
                    connection 'scm:git@git@github.com:roguedevs-com/node4ant.git'
                    developerConnection 'scm:git@git@github.com:roguedevs-com/node4ant.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'roguejammin'
                        name 'Rogue Jammin'
                    }
                    developer {
                        id 'rougemazama'
                        name 'Rogue Mazama'
                    }
                }
            }
        }
    }
}
